#!/usr/bin/env perl

use warnings;
use strict;

use Digest::MD5 qw(md5_hex);
use Digest::SHA1 qw(sha1_hex);

use lib qw(
	/home/troc/projects/git/SVN-Dump/lib
	./lib
);

use Getopt::Long;
use SVN::Dump;

my ($dump_file_name, $verbose, $help);
my $getopt_okay = GetOptions(
	'dump=s',       \$dump_file_name,
	'verbose',      \$verbose,
	'help',         \$help,
);

if ($help or !$getopt_okay) {
	die(
		"$0 usage:\n",
		"  --dump=FILENAME  location of svn dump file to replay. - for STDIN\n",
		"  --verbose        explain what's happening in great detail\n",
		"  --help           you're soaking in it.\n",
	);
}

unless (defined $dump_file_name and length $dump_file_name) {
	die "$0: --dump=FILENAME required\n";
}

if ($dump_file_name ne '-') {
	unless (-e $dump_file_name) {
		die "$0: --dump path ($dump_file_name) doesn't exist\n";
	}
	unless (-f $dump_file_name) {
		die "$0: --dump path ($dump_file_name) must be a file\n";
	}
}

my $dump = SVN::Dump->new({ file => $dump_file_name });

# Print each record.
my $sequence = 0;

my $record;
while ($record = $dump->next_record()) {
	snub_record($record);
	snub_record($record->get_included_record()) if $record->get_included_record();
	print $record->as_string();
}

my %old_to_new;

sub snub_record {
	my $record = shift;

	my $header = $record->get_headers_block();

	if ($verbose and $record->type() eq "revision") {
		warn "Revision ", $header->{'Revision-number'}, "...\n";
	}

	if (exists $header->{'Text-copy-source-md5'}) {
		$header->{'Text-copy-source-md5'} = $old_to_new{
			$header->{'Text-copy-source-md5'}
		};
	}

	return unless $record->has_text();

	my $text = "snubbed " . ++$sequence;

	$record->set_text($text);

	my $old_md5 = $header->{'Text-content-md5'};
	my $new_md5 = md5_hex($text);

	$header->{'Text-content-md5'} = $new_md5;
	$old_to_new{$old_md5} = $new_md5;
}
